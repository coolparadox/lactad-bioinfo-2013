#!/usr/bin/env perl

#
# Answer the reverse complement of a DNA sequence.
#
# This routine was developed as a homework exercise for LaCTAD Bioinformatics Course VI.
# By Rafael Lorandi <coolparadox@gmail.com>.
#

use strict;
use warnings;
use feature 'say';

use Getopt::Long;

# Online help definitions.

my $version = "0.1";

my $usage_msg =<<__eof__;
usage: dna_complement -h
   or: dna_complement [OPTIONS...] [SEQUENCE]
__eof__

my $help_msg=<<__eof__;
$usage_msg
Calculate the reverse complement of a DNA sequence.

  -h, --help      This help screen.
  -v, --version   Version number of this routine.

SEQUENCE is a DNA sequence. If absent, sequence is read from STDIN.

Answer is sent to STDOUT.

Report bugs to Rafael Lorandi <coolparadox\@gmail.com>.
__eof__

# (Subroutine) Show usage hint and fail.
sub fail_with_usage {

	print STDERR $usage_msg;
	exit 1;

}

#
# Parse CLI options.
#

my ($help_wanted, $version_wanted);
my $options_parsed = GetOptions(

	'help' => \$help_wanted,
	'version' => \$version_wanted,

);
if (!$options_parsed) {

	# Failed to parse all CLI options.
	fail_with_usage;

}
if ($help_wanted) {

	# User wants online help.
	print $help_msg;
	exit 0;
}
if ($version_wanted) {

	# User wants routine version number.
	say $version;
	exit 0;
}
my $dna_sequence = shift;
if (@ARGV) {

	# Too many arguments.
	say STDERR "error: too many arguments.";
	fail_with_usage;

}

#
# Process DNA sequence.
#

# (Subroutine) Does a string represent DNA?
sub is_dna {

	my ($sequence) = @_;
	return !($sequence =~ m/[^aAcCgGtT]/);

}

# (Subroutine) Reverse complement a DNA sequence.
sub dna_complement {

	my ($sequence) = @_;
	$sequence =~ tr/aAcCgGtT/tTgGcCaA/;
	return scalar reverse $sequence;

}

if (defined $dna_sequence) {

	# User passed DNA sequence in command line.
	chomp $dna_sequence;
	if (!is_dna $dna_sequence) {

		say STDERR "error: invalid DNA sequence.";
		exit 1;

	}
	say STDOUT dna_complement $dna_sequence;

}
else {

	# User did not pass DNA sequence in command line.
	# Let's get it from STDIN.
	my @sequences;
	while (<STDIN>) {

		chomp $_;
		if (!is_dna $_) {

			say STDERR "error: invalid DNA sequence.";
			exit 1;

		}
		push @sequences, $_;

	}
	while (@sequences) {

		print STDOUT dna_complement pop @sequences;

	}
	print "\n";

}
